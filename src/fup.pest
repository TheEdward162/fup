// FUP - Fuck Unclosed Parens

Input = _{ SOI ~ FupExpression+ ~ EOI }

// Expression is a set of terms with operators infixed in between
FupExpression = {
	FupTerm ~ (FupInfixOp ~ FupTerm)*
}

// Atomic FUP expression
FupTerm = {
	FupDefine |
	FupLetAny |
	FupCall |
	FupIndex |

	"{" ~ FupExpression ~ "}" |
	FupCond |
	FupList |

	SchemeExpression |

	Number |
	Boolean |
	Character |
	String |
	Name
}

// DEFINE
FnParameters = {
	(Name ~ ("," ~ Name)* ~ ","?)?
}
FupDefine = {
	"define" ~ Name ~ "=" ~ FupExpression ~ ";" |
	"define" ~ Name ~ "(" ~ FnParameters ~ ")" ~ "{" ~ FupExpression* ~ "}"
}

// LET
FupLetAssignments = {
	Name ~ "=" ~ FupExpression ~ ("," ~ FupLetAssignments)*
}
FupLet = {
	"let" ~ FupLetAssignments ~ FupExpression*
}
FupLetRec = {
	"letrec" ~ FupLetAssignments ~ FupExpression*
}
FupLetStar = {
	"let*" ~ FupLetAssignments ~ FupExpression*
}
FupLetAny = {
	FupLetStar |
	FupLetRec |
	FupLet
}

// CALL
FnArguments = {
	(FupExpression ~ ("," ~ FupExpression)+ ~ ","?) | (FupExpression ~ ",") | ","
}
FupCall = {
	Callable ~ "(" ~ FnArguments ~ ")"
}
Callable = {
	"{" ~ FupExpression ~ "}" |
	SchemeExpression |
	Name
}

// INDEX
FupIndex = {
	Indexable ~ "[" ~ (FupIndexFull | FupIndexOpenLeft | FupIndexOpenRight | FupIndexExact) ~ "]"
}
FupIndexExact = {
	IndexNumber
}
FupIndexOpenRight = {
	IndexNumber ~ ".."
}
FupIndexOpenLeft = {
	".." ~ IndexNumber
}
FupIndexFull = {
	IndexNumber ~ ".." ~ IndexNumber
}
IndexNumber = @{
	Digit+
}
Indexable = {
	"{" ~ FupExpression ~ "}" |
	SchemeExpression |
	Name
}

// COND
FupCond = {
	"cond" ~ "{" ~ (CondArm ~ ("," ~ CondArm)* ~ ","?)? ~ "}"
}
CondArm = {
	FupExpression ~ "=>" ~ FupExpression
}

// List literal
FupList = {
	"[" ~ "]" |
	"[" ~ FupExpression ~ "," ~ "]" |
	"[" ~ FupExpression ~ ("," ~ FupExpression)+ ~ ","? ~ "]"
}

// Infix operators
FupInfixOp = _{
	OpAppend |
	OpCons |
	OpAdd |
	OpSubtract |
	OpMultiply |
	OpDivide |
	OpModulo |
	OpAnd |
	OpOr |
	OpLowerThanOrEqual |
	OpGreaterThanOrEqual |
	OpLowerThan |
	OpGreaterThan |
	OpEq |
	OpEqv |
	OpEqual |
	OpMathEqual
}
OpAppend = { "++" }
OpCons = { ":" }
OpAdd = { "+" }
OpSubtract = { "-" }
OpMultiply = { "*" }
OpDivide = { "/" }
OpModulo = { "%" }
OpAnd = { "and" }
OpOr = { "or" }
OpLowerThanOrEqual = { "<=" }
OpGreaterThanOrEqual = { ">=" }
OpLowerThan = { "<" }
OpGreaterThan = { ">" }
OpEq = { "is" | "eq?" }
OpEqv = { "eqv?" }
OpEqual = { "==" | "equal?" }
OpMathEqual = { "=" }

// FUP names, subset of Scheme identifiers without special symbols (operators)
Name = @{
	Letter ~ ( "_" | "?" | Letter | Digit )*
}

// Scheme rules
SchemeExpression = {
	"(" ~ ( FupTerm | SchemeExpression | Atom )* ~ ")"
}

Atom = ${
	Number |
	Boolean |
	Character |
	String |
	Identifier
}

Number = @{
	Digit+ ~ ("." ~ Digit*)?
}

// Scheme identifier
Identifier = @{
	(Special | Letter) ~ (Special | Letter | Digit)*
}

Boolean = @{
	"#t" | "#f"
}

Character = @{
	"#\\" ~ CharacterName | "#\\" ~ ANY
}
CharacterName = {
	"space" | "newline"
}

String = @{
	"\"" ~ StringElement* ~ "\""
}
StringElement = _{
	"\\" | "\\\"" | !("\"" | "\\") ~ ANY
}

Special = _{
	"!" | "\"" | "#" | "$" | "%" | "&" | "*" | "+" | "-" | "." | "/" |
	":" | "<" | "=" | ">" | "?" | "@" | "^" | "_" | "|" | "~"
}
Digit = _{ ASCII_DIGIT }
Letter = _{ 'a'..'z' | 'A'..'Z' }

COMMENT = _{ ";" ~ (!"\n" ~ ANY)* ~ ("\n" | EOI) }
WHITESPACE = _{ " " | "\t" | "\n" }
