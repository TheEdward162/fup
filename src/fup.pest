// FUP - Fuck Unclosed Parens

Input = _{ SOI ~ OperatorExpression+ ~ EOI }

// Expression of infix operators.
OperatorExpression = {
	FupExpression ~ (FupInfixOp ~ FupExpression)*
}

// Expression which cannot be direcly embedded in Scheme because it would be ambiguous.
// Contains Name because Identifier would be ambiguous in FUP.
FupExpression = {
	FupLambda |
	FupTerm |
	Name
}

// Expression which is unambiguous in Scheme expressions.
FupTerm = {
	FupList |
	FupQuote |
	
	FupLetAny |
	FupIf |
	FupDefine |
	FupCond |
	
	FupIndex |
	FupCall |

	SchemeExpression |
	"{" ~ OperatorExpression ~ "}" |

	Number |
	Boolean |
	Character |
	String
}

// QUOTE
FupQuote = @{
	"'" ~ "(" ~ FupQuoteInner* ~ ")" |
	"`" ~ "(" ~ FupQuoteInner* ~ ")" |
	"'" ~ Identifier
}
FupQuoteInner = @{
	(!("(" | ")") ~ ANY)+ |
	"(" ~ FupQuoteInner* ~ ")"
}

// DEFINE
FnParameters = {
	(Name ~ ("," ~ Name)* ~ ","?)?
}
FupDefine = {
	"define" ~ Name ~ "=" ~ OperatorExpression ~ ";" |
	"define" ~ Name ~ "(" ~ FnParameters ~ ")" ~ "{" ~ OperatorExpression* ~ "}"
}

// LET
FupLetAssignments = {
	Name ~ "=" ~ OperatorExpression ~ ("," ~ FupLetAssignments)*
}
FupLet = {
	"let" ~ FupLetAssignments ~ OperatorExpression*
}
FupLetRec = {
	"letrec" ~ FupLetAssignments ~ OperatorExpression*
}
FupLetStar = {
	"let*" ~ FupLetAssignments ~ OperatorExpression*
}
FupLetAny = {
	FupLetStar |
	FupLetRec |
	FupLet
}

// IF
FupIf = {
	"if" ~ OperatorExpression ~ "{" ~ OperatorExpression ~ "}" ~ "else" ~ "{" ~ OperatorExpression ~ "}"
}

// LAMBDA
FupLambda = {
	"lambda" ~ "(" ~ FnParameters ~ ")" ~ OperatorExpression
}

// CALL
FnArguments = {
	(OperatorExpression ~ ("," ~ OperatorExpression)+ ~ ","?) | (OperatorExpression ~ ",") | ","
}
FupCall = {
	Callable ~ "(" ~ FnArguments ~ ")"
}
Callable = {
	"{" ~ OperatorExpression ~ "}" |
	SchemeExpression |
	Name
}

// INDEX
FupIndex = {
	Indexable ~ "[" ~ (FupIndexFull | FupIndexOpenLeft | FupIndexOpenRight | FupIndexExact) ~ "]"
}
FupIndexExact = {
	IndexNumber
}
FupIndexOpenRight = {
	IndexNumber ~ ".."
}
FupIndexOpenLeft = {
	".." ~ IndexNumber
}
FupIndexFull = {
	IndexNumber ~ ".." ~ IndexNumber
}
IndexNumber = @{
	Digit+
}
Indexable = {
	"{" ~ OperatorExpression ~ "}" |
	SchemeExpression |
	Name
}

// COND
FupCond = {
	"cond" ~ "{" ~ (CondArm ~ ("," ~ CondArm)* ~ ","?)? ~ "}"
}
CondArm = {
	OperatorExpression ~ "=>" ~ OperatorExpression
}

// List literal
FupList = {
	"[" ~ "]" |
	"[" ~ OperatorExpression ~ "," ~ "]" |
	"[" ~ OperatorExpression ~ ("," ~ OperatorExpression)+ ~ ","? ~ "]"
}

// Infix operators
FupInfixOp = _{
	OpAppend |
	OpCons |
	OpAdd |
	OpSubtract |
	OpMultiply |
	OpDivide |
	OpModulo |
	OpAnd |
	OpOr |
	OpLowerThanOrEqual |
	OpGreaterThanOrEqual |
	OpLowerThan |
	OpGreaterThan |
	OpEq |
	OpEqv |
	OpEqual |
	OpMathEqual
}
OpAppend = { "++" }
OpCons = { ":" }
OpAdd = { "+" }
OpSubtract = { "-" }
OpMultiply = { "*" }
OpDivide = { "/" }
OpModulo = { "%" }
OpAnd = { "and" }
OpOr = { "or" }
OpLowerThanOrEqual = { "<=" }
OpGreaterThanOrEqual = { ">=" }
OpLowerThan = { "<" }
OpGreaterThan = { ">" }
OpEq = { "is" | "eq?" }
OpEqv = { "eqv?" }
OpEqual = { "==" | "equal?" }
OpMathEqual = { "=" }

// FUP names, subset of Scheme identifiers without special symbols (operators)
Name = @{
	Letter ~ ( "_" | "?" | Letter | Digit )*
}

// Scheme rules
SchemeExpression = {
	"(" ~ ( FupTerm | Atom )* ~ ")"
}

Atom = ${
	Number |
	Boolean |
	Character |
	String |
	Identifier
}

Number = @{
	Digit+ ~ ("." ~ Digit*)?
}

// Scheme identifier
Identifier = @{
	(Special | Letter) ~ (Special | Letter | Digit)*
}

Boolean = @{
	"#t" | "#f"
}

Character = @{
	"#\\" ~ CharacterName | "#\\" ~ ANY
}
CharacterName = {
	"space" | "newline"
}

String = @{
	"\"" ~ StringElement* ~ "\""
}
StringElement = _{
	"\\" | "\\\"" | !("\"" | "\\") ~ ANY
}

Special = _{
	"!" | "\"" | "#" | "$" | "%" | "&" | "*" | "+" | "-" | "." | "/" |
	":" | "<" | "=" | ">" | "?" | "@" | "^" | "_" | "|" | "~"
}
Digit = _{ ASCII_DIGIT }
Letter = _{ 'a'..'z' | 'A'..'Z' }

COMMENT = _{ ";" ~ (!"\n" ~ ANY)* ~ ("\n" | EOI) }
WHITESPACE = _{ " " | "\t" | "\n" }
