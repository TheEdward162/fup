// FUP - Fuck Unclosed Parens

Input = _{ SOI ~ FupExpression+ ~ EOI }

// FUP rules
FupExpression = {
	FupDefine |
	FupTerm ~ (FupInfixOp ~ FupTerm)*
}

// Atomic FUP expression
FupTerm = {
	FupCall |
	FupIndex |
	
	"{" ~ FupExpression ~ "}" |
	FupCond |

	SchemeExpression |

	Number |
	Boolean |
	Character |
	String |
	Name
}

// Parameters for a function definition
FnParameters = {
	(Name ~ ("," ~ Name)* ~ ","?)?
}

// Define special form, with shortcut for function definition
FupDefine = {
	"define" ~ Name ~ "=" ~ FupExpression ~ ";" |
	"define" ~ Name ~ "(" ~ FnParameters ~ ")" ~ "{" ~ FupExpression* ~ "}"
}

// Arguments for function call
FnArguments = {
	(FupExpression ~ ("," ~ FupExpression)+ ~ ","?) | (FupExpression ~ ",") | ","
}

// Function call
FupCall = {
	Callable ~ "(" ~ FnArguments ~ ")"
}
Callable = {
	"{" ~ FupExpression ~ "}" |
	SchemeExpression |
	Name
}

// List indexing
FupIndex = {
	Indexable ~ "[" ~ (FupIndexFull | FupIndexOpenLeft | FupIndexOpenRight | FupIndexExact) ~ "]"
}
FupIndexExact = {
	IndexNumber
}
FupIndexOpenRight = {
	IndexNumber ~ ".."
}
FupIndexOpenLeft = {
	".." ~ IndexNumber
}
FupIndexFull = {
	IndexNumber ~ ".." ~ IndexNumber
}
IndexNumber = @{
	Digit+
}
Indexable = {
	"{" ~ FupExpression ~ "}" |
	SchemeExpression |
	Name
}

// Cond expression
FupCond = {
	"cond" ~ "{" ~ (CondArm ~ ("," ~ CondArm)* ~ ","?)? ~ "}"
}

// One arm of cond expression
CondArm = {
	FupExpression ~ "=>" ~ FupExpression
}

// Infix operators
FupInfixOp = _{
	OpAppend |
	OpCons |
	OpAdd |
	OpSubtract |
	OpMultiply |
	OpDivide |
	OpModulo |
	OpAnd |
	OpOr |
	OpLowerThanOrEqual |
	OpGreaterThanOrEqual |
	OpLowerThan |
	OpGreaterThan |
	OpEq |
	OpEqv |
	OpEqual |
	OpMathEqual
}
OpAppend = { "++" }
OpCons = { ":" }
OpAdd = { "+" }
OpSubtract = { "-" }
OpMultiply = { "*" }
OpDivide = { "/" }
OpModulo = { "%" }
OpAnd = { "and" }
OpOr = { "or" }
OpLowerThanOrEqual = { "<=" }
OpGreaterThanOrEqual = { ">=" }
OpLowerThan = { "<" }
OpGreaterThan = { ">" }
OpEq = { "is" | "eq?" }
OpEqv = { "eqv?" }
OpEqual = { "==" | "equal?" }
OpMathEqual = { "=" }

// FUP names, subset of Scheme identifiers without special symbols (operators)
Name = @{
	Letter ~ ( "_" | Letter | Digit )*
}


// Scheme rules
SchemeExpression = {
	"(" ~ ( FupTerm | SchemeExpression | Atom )* ~ ")"
}

Atom = ${
	Number |
	Boolean |
	Character |
	String |
	Identifier
}

Number = @{
	Digit+ ~ ("." ~ Digit*)?
}

// Scheme identifier
Identifier = @{
	(Special | Letter) ~ (Special | Letter | Digit)*
}

Boolean = @{
	"#t" | "#f"
}

Character = @{
	"#\\" ~ CharacterName | "#\\" ~ ANY
}
CharacterName = {
	"space" | "newline"
}

String = @{
	"\"" ~ StringElement* ~ "\""
}
StringElement = _{
	"\\" | "\\\"" | !("\"" | "\\") ~ ANY
}

Special = _{
	"!" | "\"" | "#" | "$" | "%" | "&" | "*" | "+" | "-" | "." | "/" |
	":" | "<" | "=" | ">" | "?" | "@" | "^" | "_" | "|" | "~"
}
Digit = _{ ASCII_DIGIT }
Letter = _{ 'a'..'z' | 'A'..'Z' }

COMMENT = _{ ";" ~ (!"\n" ~ ANY)* ~ ("\n" | EOI) }
WHITESPACE = _{ " " | "\t" | "\n" }
