// FUP - Fuck Unclosed Parens

Input = _{ SOI ~ AugmentedExpression+ ~ EOI }

AugmentedExpression = _{
	FupExpression |
	SchemeExpression |
	Atom
}

// FUP rules
FupExpression = {
	FupDefine
	// TODO: cond, list indexing, more..
}

// Arguments for a function definition, call, etc.
FnArguments = {
	(Name ~ ("," ~ Name)*)?
}
// Body of a block enclosed in `{}`
BlockBody = {
	AugmentedExpression*
}

// Define `
FupDefine = {
	"define" ~ Name ~ "=" ~ AugmentedExpression ~ ";" |
	"define" ~ Name ~ "(" ~ FnArguments ~ ")" ~ "{" ~ BlockBody ~ "}"
}

// Scheme rules
SchemeExpression = {
	"(" ~ ( Atom | SchemeExpression )* ~ ")"
}

Atom = ${
	Number |
	Boolean |
	Character |
	String |
	Identifier
}

Number = @{
	Digit+ ~ ("." ~ Digit*)?
}
Name = @{
	Letter ~ ( "_" | Letter | Digit )*
}
Identifier = @{
	(Special | Letter) ~ (Special | Letter | Digit)*
}

Boolean = @{
	"#t" | "#f"
}

Character = @{
	"#\\" ~ CharacterName | "#\\" ~ ANY
}
CharacterName = {
	"space" | "newline"
}

String = @{
	"\"" ~ StringElement* ~ "\""
}
StringElement = _{
	"\\" | "\\\"" | !("\"" | "\\") ~ ANY
}

Special = _{
	"!" | "\"" | "#" | "$" | "%" | "&" | "*" | "+" | "," | "-" | "." |
	"/" | ":" | "<" | "=" | ">" | "?" | "@" | "^" | "_" | "|" | "~"
}
Digit = _{ ASCII_DIGIT }
Letter = _{ 'a'..'z' | 'A'..'Z' }

COMMENT = _{ ";" ~ (!"\n" ~ ANY)* ~ ("\n" | EOI) }
WHITESPACE = _{ " " | "\t" | "\n" }
